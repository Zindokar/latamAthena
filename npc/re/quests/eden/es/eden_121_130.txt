//===== rAthena Script =======================================
//= Eden Group Quests - Mission [121-130]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 121 - 130.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,81,3	script	Melody-Jack#1	4_M_MELODY,8,8,{
mes "[Melodía-Jack]";
	if (!callfunc("F_HasEdenGroupMark")) {
mes "¿Quién demonios eres tú? ¡Largo de aquí!";
mes "Solo estoy hablando con los aventureros de nivel base ^FF0000121-130^000000.";
	}
	else if (para_120lv01 == 0)
mes "¿Qué? no me molestes. Ahora estoy ocupado.";
	else if (para_120lv01 == 1) {
mes "¿Tienes una petición de esa niña? ¿El pequeño, parlanchín y semidesnudo de allí?";
		next;
		select("Decir sí.", "Evadir responder.");
mes "[Melodía-Jack]";
mes "No. No me importa, estará bien si entras en esta habitación, no porque estés perdido. Necesito a cualquiera, excepto a ese tipo de tonto.";
		next;
mes "[Melodía-Jack]";
mes "Por favor, escúchame, te ofreceré una sugerencia atractiva.";
		next;
mes "[Melodía-Jack]";
mes "Soy ^ff0000Melody - Jack!^000000 El mejor hombre con las mejores habilidades con la daga.";
		next;
mes "Melody-Jack desliza una daga en tu dirección.";
		next;
mes "[Melodía-Jack]";
mes "Estoy calificado para tomar misiones secretas. Y ahora mismo estás de suerte.";
		next;
mes "[Melodía-Jack]";
mes "Parece que tengo que perder una misión, trato y promesa, no puedo tomarla en este momento.";
		next;
mes "[Melodía-Jack]";
mes "Toma mi misión y resuelve la solicitud de Aigu. Te compensaré.";
		next;
mes "[Melodía-Jack]";
mes "Nuestro acuerdo se mantendrá en secreto para Aigu. Si no tienes 9 vidas. Empecemos.";
		para_120lv01 = 2;
	}
	else if (para_120lv01 > 1) {
mes "......";
		next;
		if (select( "Show me the requests!", "Make small-talk." ) == 2) {
mes "[Melodía-Jack]";
mes "¿Qué? ¿Te interesa yo?";
			next;
			switch( select( "I am interested.", "About requests", "About a deal", "What is a promise?" ) ) {
mes "[Melodía-Jack]";
			case 1:
mes "Bueno, vete. No tengo ningún interés en ti.";
				close;
			case 2:
mes "No puedo detenerlo debido a mi mal crédito. Me vuelve loco.";
				next;
mes "[Melodía-Jack]";
mes "Tengo la suerte de hacer un doble contrato con un aventurero como tú. No están incluidos en nuestro contrato.";
				close;
			case 3:
mes "Para ser honesto, no creo que haya nadie digno de confianza. La confianza o las promesas son tonterías.";
				next;
mes "[Melodía-Jack]";
mes "Solo confío en mí y en mi trato. La confianza o las promesas son vacías si se rompen, pero si se rompe un trato, hay compensación o venganza.";
				close;
			case 4:
mes "¿Quiénes son todos estos niños?";
				next;
mes "[Melodía-Jack]";
mes "Salí por nada, pero se supone que debo cumplir con mi deber. No le debo nada a nadie.";
				close;
			}
		}
mes "[Melodía-Jack]";
		if (BaseLevel < 121)
mes "Oh, cometí un error. No eres el nivel de aventurero que estoy buscando. Estoy buscando aventureros de nivel base 121 a 130.";
		else if (BaseLevel < 131) {
mes "No me gusta, pero un trato es un trato. Por favor, regresa después de eliminar monstruos en los lugares donde Aigu pidió buscar.";
			next;
			switch( select( "Thanatos Tower related ", "Rachel Shrine related ", "Nameless Island related ", "Abyss Lake related " ) ) {
mes "[Melodía-Jack]";
			case 1:
mes "La torre de Tánatos es el lugar más molesto. No te preocupes por tu compensación. Cumplo mi palabra.";
				next;
				switch( select( "Baroness of Retribution", "Lady Solace", "Mistress of Shelter", "Dame of Sentinel" ) ) {
mes "[Melodía-Jack]";
				case 1:
					if (checkquest(13120,PLAYTIME) == 0)
mes "Creo que terminé la actuación de hoy. No puedo relajarme si los monstruos aparecen de repente.";
					else if (checkquest(13119,HUNTING) == 1) {
mes "No me gusta esa criatura, no porque a Aigu no le gusten... ¿Cómo te atreves? ¿Quién castiga a quién?";
						next;
mes "[Melodía-Jack]";
mes "Estas son algunas de las criaturas más peligrosas del mundo. Estas criaturas no merecen justicia.";
						next;
						cutin "",255;
mes "[Melodía-Jack]";
mes "Por supuesto que puedes confiar en mí. Porque creo en los negocios reales. Vamos a barrer a las criaturas.";
					}
					else if (checkquest(13119,HUNTING) == 2) {
mes "¿Los borraste? Así que eso no molesta a ese niño por un día. Buen trabajo.";
						callsub S_Quest,13119,13120,1;
					}
					else {
mes "Como sabes, la Torre Tánatos es el lugar donde más le importa a Aigu. Es el lugar más peligroso en algunos aspectos.";
						next;
mes "[Melodía-Jack]";
mes "Por favor, elimina monstruos para ayudar a Aigu. Los aficionados harán las cosas fáciles.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Debe haber muchos de ellos. No olvides mantener esto en secreto.";
							callsub S_Quest,13120,13119;
						}
					}
					close;
				case 2:
					if (checkquest(13122,PLAYTIME) == 0)
mes "Creo que terminé la actuación de hoy. No puedo relajarme... ¿Y si los monstruos aparecen de repente?";
					else if (checkquest(13121,HUNTING) == 1) {
mes "El edificio no parece haber sido diseñado por seres humanos y siempre suena allí una extraña melodía.";
						next;
mes "[Melodía-Jack]";
mes "No caigas en el estado de ánimo de la melodía porque puede ser la última canción que escuches.";
						next;
						cutin "",255;
mes "[Melodía-Jack]";
mes "Recuerda, hay una música extraña tan afilada como un cuchillo para matar personas en el mundo.";
					}
					else if (checkquest(13121,HUNTING) == 2) {
mes "¿Alguna vez has escuchado la melodía que están tocando? Tendrás una oportunidad si ese niño comienza a tocar la actuación de nuevo.";
						callsub S_Quest,13121,13122,1;
					}
					else {
mes "Hermosa melodía. Tal vez suene hermoso porque las criaturas están jugando.";
						next;
mes "[Melodía-Jack]";
mes "Sin embargo, ese es otro obstáculo. Un trato es previo al interés personal en cualquier momento. Tan claro Lady Solace.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Simplemente estamos cumpliendo nuestra parte del trato. Elimina a esas criaturas ahora.";
							callsub S_Quest,13122,13121;
						}
					}
					close;
				case 3:
					if (checkquest(13124,PLAYTIME) == 0)
mes "Creo que terminé la actuación de hoy. No puedo relajarme... ¿Y si los monstruos aparecen de repente?";
					else if (checkquest(13123,HUNTING) == 1) {
mes "¿Conoces a un ángel que se convirtió en un monstruo? No sé si era un ángel o no. Pero su apariencia era la de un ángel.";
						next;
mes "[Melodía-Jack]";
mes "No entres en pánico debido a las hermosas apariencias, independientemente de si se trata de un ángel o una criatura. Debes hacer tu trabajo.";
						next;
mes "[Melodía-Jack]";
mes "Todo tiene su tiempo. Tal vez lo solicitaste porque ese momento ha llegado.";
						cutin "",255;
					}
					else if (checkquest(13123,HUNTING) == 2) {
mes "Bien. Avísame si alguien me está buscando. Si es así, es posible que tenga que cambiar los detalles del trato.";
						callsub S_Quest,13123,13124,1;
					}
					else {
mes "¿Conoces a un ángel que se convirtió en un monstruo? No sé si era un ángel o no. Pero su apariencia era la de un ángel.";
						next;
mes "[Melodía-Jack]";
mes "No me importa si es un ángel real o simplemente una criatura. Clara dueña de la casa.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Las flores moribundas caen por la lluvia y el viento. Eso es lo que tienes que hacer.";
							emotion ET_BEST;
							callsub S_Quest,13124,13123;
						}
					}
					close;
				case 4:
					if (checkquest(13126,PLAYTIME) == 0)
mes "Terminé mi actuación de hoy. No puedo relajarme si los monstruos aparecen de repente.";
					else if (checkquest(13125,HUNTING) == 1) {
mes "Es posible que hayas sentido que te están mirando una vez que pisaste el área superior de la Torre Thanatos.";
						next;
mes "[Melodía-Jack]";
mes "Dame of Sentinel no es fácil de manejar, así que asegúrate de estar listo para luchar.";
						next;
mes "[Melodía-Jack]";
mes "Es un buen día para morir. Serás tú o ellos, así que haz lo mejor que puedas.";
						cutin "",255;
					}
					else if (checkquest(13125,HUNTING) == 2) {
mes "Te ves bien. ¿Quizás eres mejor de lo que pensaba? Sin embargo, no lo comprobé con mis ojos.";
						callsub S_Quest,13125,13126,1;
					}
					else {
mes "Se reunirá con ellos si Aigu lo solicita. La Dama de los Centinelas.";
						next;
mes "[Melodía-Jack]";
mes "Deseo que también estén despejados para que ella pueda solicitar esta aventura fácilmente.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Una solicitud de una solicitud... Lo que sea.";
							callsub S_Quest,13126,13125;
						}
					}
					close;
				}
			case 2:
mes "La aventura de Aigu conduce al Santuario de Raquel. Bien... No es extraño que ^FF0000ese humano^000000 Aigu buscando se encuentre en cualquier parte.";
				next;
				switch( select( "Vanberk and Isilla", "Hodremlin", "Agav and Echio" ) ) {
mes "[Melodía-Jack]";
				case 1:
					if (checkquest(13128,PLAYTIME) == 0)
mes "Puede tomar un descanso por un día. Has despejado lo suficiente, pero el fanático ha aumentado.";
					else if (checkquest(13127,HUNTING) == 1) {
mes "Alguien que cree que tiene razón es el más peligroso. Este mundo está lleno de tontos.";
						next;
mes "[Melodía-Jack]";
mes "Finalmente, obliga a los demás a creer lo que ellos creen. Vamos a despejar Vanberk e Isilla.";
						next;
mes "[Melodía-Jack]";
mes "No es asunto mío. Pero esta vez es diferente. Un niño pequeño podría estar en peligro, así que apresurémonos a eliminar a estos monstruos.";
						cutin "",255;
					}
					else if (checkquest(13127,HUNTING) == 2) {
mes "Solo un día. Estará bien.";
						callsub S_Quest,13127,13128,1;
					}
					else {
mes "El Santuario en Rachel está prohibido para la gente normal. Tienes que saberlo.";
						next;
mes "[Melodía-Jack]";
mes "Creen que pueden encontrar una pista allí, pero no es un lugar seguro. Debes despejar Vanberk e Isilla con anticipación.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Borra todo lo más rápido que puedas.";
							emotion ET_SPARK;
							callsub S_Quest,13128,13127;
						}
					}
					close;
				case 2:
					if (checkquest(13130,PLAYTIME) == 0)
mes "Los mosmos desconocidos son peligrosos, así que elimínalos primero. Te informaré si pasa algo.";
					else if (checkquest(13129,HUNTING) == 1) {
mes "Es posible que se acerquen los dientes fríos. Sus dientes son más rápidos que tus manos.";
						next;
mes "[Melodía-Jack]";
mes "El santuario de Rachel se acercará desde todas partes. Debes tener cuidado con este.";
						next;
mes "[Melodía-Jack]";
mes "Solo piensa en el trabajo que tienes entre manos. Con eso basta.";
						cutin "",255;
					}
					else if (checkquest(13129,HUNTING) == 2) {
mes "No estoy tan preocupado, pero ¿estás a salvo? ¿No te ha mordido?";
						callsub S_Quest,13129,13130,1;
					}
					else {
mes "Los fanáticos son un problema. Un problema más grave es que aparecen de repente.";
						next;
mes "[Melodía-Jack]";
mes "Deshazte de ^FF0000Hodremlin^000000 sin dudarlo. Porque son muy peligrosos.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "Necesitan manos más rápidas que una cabeza. No me preocupo, pero debes tener cuidado.";
							callsub S_Quest,13130,13129;
						}
					}
					close;
				case 3:
					if (checkquest(13132,PLAYTIME) == 0)
mes "Si puedes hablar. No hay solicitudes en este momento. Tómate un descanso por el día.";
					else if (checkquest(13131,HUNTING) == 1) {
mes "Lleno de locura aquí. Me siento repugnante.";
						next;
mes "[Melodía-Jack]";
mes "Es mi sensación, pero puede que te guste. Estoy seguro de que ^FF0000Agav^000000 y ^FF0000Echio^000000 no te caerán bien.";
						next;
mes "[Melodía-Jack]";
mes "Se toparán contigo. No lo dudes y mantente enfocado en el trabajo.";
						cutin "",255;
					}
					else if (checkquest(13131,HUNTING) == 2) {
mes "¿Encontraste a alguien? No me preocuparía. Una promesa es una promesa y un trato es un trato.";
						callsub S_Quest,13131,13132,1;
					}
					else {
mes "Es cierto que se está volviendo más peligroso dentro del santuario de Rachel, pero a Aigu no le importarán las cosas de las que no estamos seguros.";
						next;
mes "[Melodía-Jack]";
mes "Eso no fue tan malo. ¿Pero alguien tiene que ayudar? Despejemos ^FF0000Agav^000000 y ^FF0000Echio^000000 dentro del Santuario de Raquel.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
mes "[Melodía-Jack]";
mes "No puedo irme tan adiós.";
							callsub S_Quest,13132,13131;
						}
					}
					close;
				}
			case 3:
				mes "Nameless Island is not known to usual adventurers. Aigu is also interested in there.";
				next;
				switch( select( "Ragged Zombie Hunting", "Zombie Slaughter Hunting", "Banshee Hunting" ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13134,PLAYTIME) == 0)
						mes "Ragged Zombie is reduced. It is a good time to proceed Aigu's request for a minute.";
					else if (checkquest(13133,HUNTING) == 1) {
						mes "It is easy to meet everything on Nameless Island. So boring.";
						next;
						mes "[Melody-Jack]";
						mes "Get rid of it and help Aigu and the other adventurers.";
						cutin "",255;
					}
					else if (checkquest(13133,HUNTING) == 2) {
						mes "Did you take a bath? It smells in here. But I am sure that you had a hard time with the smell.";
						callsub S_Quest,13133,13134,1;
					}
					else {
						mes "The dead come back at night. Comes back sounds strange. Lets cut the crap.";
						next;
						mes "[Melody-Jack]";
						mes "Wish to clear ^FF0000Ragged Zombie^000000. It is easy because they show up everywhere.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Take a ^FF0000bath^000000 and you won't smell anymore.";
							callsub S_Quest,13134,13133;
						}
					}
					close;
				case 2:
					if (checkquest(13136,PLAYTIME) == 0)
						mes "I know the request to Nameless Island is not easy but I will be happy if you join again.";
					else if (checkquest(13135,HUNTING) == 1) {
						mes "It was not easy for me either to step into Nameless Island. I am not that patient.";
						next;
						mes "[Melody-Jack]";
						mes "But adventurers should challenge once. ^FF0000Zombie Slaughter^000000 is worth clearing.";
						cutin "",255;
					}
					else if (checkquest(13135,HUNTING) == 2) {
						mes "You are quite good! You cleared them. It will take time to return.";
						callsub S_Quest,13135,13136,1;
					}
					else {
						mes "They are dangerous unlike Ragged Zombie because they swing something sharp.";
						next;
						mes "[Melody-Jack]";
						mes "Please clear ^FF0000Zombie Slaughter^000000. Be careful to avoid their blades.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "You've got many things to take care of so I will hold my words. Clear them.";
							callsub S_Quest,13136,13135;
						}
					}
					close;
				case 3:
					if (checkquest(13138,PLAYTIME) == 0)
						mes "Banshee is cleared. Request is solved anyway but it is not the end. It will be required again sooner or later.";
					else if (checkquest(13137,HUNTING) == 1) {
						mes "You will see there are unwelcoming things while adventuring sometimes regardless of monsters and companions.";
						next;
						mes "[Melody-Jack]";
						mes "This request is like that for me. I just do not like creatures or the reason of giving this request.";
						cutin "",255;
					}
					else if (checkquest(13137,HUNTING) == 2) {
						mes "You must be tired. You went to that tough place beyond your capability. Do not be angry. I will reward you for that.";
						callsub S_Quest,13137,13138,1;
					}
					else {
						mes "Please clear ^FF0000Banshee^000000 inside of Nameless Island. Do not say no. You know it is for Aigu as well.";
						next;
						mes "[Melody-Jack]";
						mes "Both Banshee and Aigu are annoying. I would not do this request but you will be different. Right?";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "See? Someone needs this request. Clear ^FF0000Banshees^000000 at Nameless Island.";
							callsub S_Quest,13138,13137;
						}
					}
					close;
				}
			case 4:
				mes "Abyss Lake has requests from a little girl. Very dilligent.";
				next;
				if (select( "Ferus and Bewler are cleared.", "Gold and Blue Acidus are cleared." ) == 1) {
					mes "[Melody-Jack]";
					if (checkquest(13140,PLAYTIME) == 0)
						mes "Green dragons and Red dragons remind me of watermelons.";
					else if (checkquest(13139,HUNTING) == 1) {
						mes "Abyss Lake... Do you possibly know how to get in there? You are not an adventurer with that information?";
						next;
						mes "[Melody-Jack]";
						mes "Just in case I will give you a clue that a key and an entrance are going to be found around there.";
						cutin "",255;
					}
					else if (checkquest(13139,HUNTING) == 2) {
						mes "Did you clear all of the dragons? Take a rest for a day.";
						callsub S_Quest,13139,13140,1;
					}
					else {
						mes "Abyss Lake is full of dragons in and out. Dragons are not that special but there are so many.";
						next;
						mes "[Melody-Jack]";
						mes "Which one is more dangerous between Ferus and Bewler? I do not know. Clear both of them.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Neither green nor red are disturbing. But regardless, come back after clearing all.";
							callsub S_Quest,13140,13139;
						}
					}
				}
				else {
					mes "[Melody-Jack]";
					if (checkquest(13142,PLAYTIME) == 0)
						mes "Take a rest. We should go and subdue Acidus dragons again.";
					else if (checkquest(13141,HUNTING) == 1) {
						mes "Abyss Lake is not close. But it is worth visiting.";
						next;
						mes "[Melody-Jack]";
						mes "This is technically a training exercise but just pretend you are helping someone. There will be a reward.";
						cutin "",255;
					}
					else if (checkquest(13141,HUNTING) == 2) {
						mes "You are more of a talented adventurer than I thought. If you found a tactic, take care of it next time. This study is up to you.";
						callsub S_Quest,13141,13142,1;
					}
					else {
						mes "There is a cave where dragons are living inside of Abyss Lake. There are so many but flying things are annoying.";
						next;
						mes "[Melody-Jack]";
						mes "Subdue them as best as you can. Do not mind their colors because they are all to be cleared.";
						next;
						cutin "",255;
						if (select( "Take the request.", "Stop." ) == 1) {
							mes "[Melody-Jack]";
							mes "Subdue the Gold and Blue Acidus monsters at the last level of Abyss Lake.";
							callsub S_Quest,13142,13141;
						}
					}
				}
				close;
			}
		}
		else {
			mes "You finished helping Aigu or did you quit? Anyways I have no more business for you. ";
			next;
			callsub S_Erasequest,
				13119,"Baroness of Retribution",
				13121,"Lady Solace",
				13123,"Mistress of Shelter",
				13125,"Dame of Sentinel",
				13127,"Vanberk and Isilla",
				13129,"Hodremlin",
				13131,"Agav and Echio",
				13133,"Ragged Zombie",
				13135,"Zombie Slaughter",
				13137,"Banshee",
				13139,"Ferus ",
				13141,"Acidus";
			mes "[Melody-Jack]";
			mes "I am not sure if you found what you are looking for... I cannot be involved anymore. Thanks.";
		}
	}
	close;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp 900000,200000;
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 0:
		case 1:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" is cleared. No need to have more requests.";
			break;
		case 2:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" is cleared. A deal is a deal. I need to get paid.";
			getexp 900000,200000;
			break;
		}
		erasequest .@quest_id;
		next;
	}
	return;
OnTouch_:
	if (para_120lv01 == 1) {
		emotion ET_SURPRISE;
		mes "[Melody-Jack]";
		mes "Hey there! ... Wait.?";
		close;
	}
	end;
}

moc_para01,17,77,5	script	Aigu#1	4_F_IU,{
	mes "[Aigu]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Oh, why aren't you a part of Eden Group?";
		mes "I'm only talking to base level ^FF0000121-130^000000 adventurers.";
	}
	else if (para_120lv01 == 0) {
		cutin "igu01",2;
		mes "Hi? A fine day like today. Can I ask for a favor? I heard that adventurers in Eden Group are nice.";
		next;
		if (select( "I'm not the nice kind.", "Sure, I'll help." ) == 1) {
			cutin "igu05",2;
			mes "[Aigu]";
			mes "yek.. I hate.";
		}
		else {
			cutin "igu03",2;
			mes "[Aigu]";
			mes "Really? I will introduce myself! I am the head of the ^ff0000Dandelion band Aigu.^000000";
			next;
			mes "[Aigu]";
			mes "I just came back from a local performance. There is someone I wanted to meet but is missing now so I came to Eden Group for a request.";
			next;
			mes "[Aigu]";
			mes "I just decided to make a request by myself because no one is in charge and everyone is busy. Thanks for the help.";
			para_120lv01 = 1;
		}
	}
	else if (para_120lv01 > 0) {
		cutin "igu01",2;
		mes "It is nice to come back to Midgard! Very exciting as the first day.";
		next;
		if (select( "What is your request?", "Talk about something else." ) == 2) {
			cutin "igu01",2;
			mes "[Aigu]";
			mes "Dandelion band travels all around the world and conveys healing music to people";
			next;
			cutin "igu02",2;
			mes "[Aigu]";
			mes "Actually our songs have no magic but I am sure it helps people because I am singing.";
		}
		else if (BaseLevel < 121) {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "I think it is difficult for you to get my request? For now, I think it will make more people lost. How about widening your sight by travelling?";
		}
		else if (BaseLevel < 131) {
			mes "[Aigu]";
			mes "I made a shelter for someone I am looking for and lost adventurers. I want to know which adventurer has been there.";
			next;
			switch( select( "Thanatos Tower search", "Rachel Sanctuary search", "Nameless Island search", "Abyss Lake search" ) ) {
				mes "[Aigu]";
			case 1:
				if (checkquest(13115,PLAYTIME) == 0)
					mes "I will ask to search again after for a while because there is hope since a trace of someone was found.";
				else if (isbegin_quest(13111) == 1) {
					cutin "igu04",2;
					mes "You know someone went there but you do not know who that is? Thank you. But let me ask you to search again tomorrow since we do not know who it is.";
					callsub S_Quest,13111,13115,1250000,300000;
				}
				else if (isbegin_quest(13107) == 1)
					mes "You may find shelter on top of the tower. Even that shelter only has water and a bonfire.";
				else {
					mes "Adventurers are easily lost because of dangerous monsters and the height of thanatos tower. But it isn't impossible for some adventurers so please check for me.";
					next;
					if (select( "Begin to search.", "Cancel." ) == 1) {
						mes "[Aigu]";
						mes "Thanatos tower is north of the Schwarzwald Republic! It is not far from Juno or Hugel.";
						callsub S_Quest,13115,13107;// Thanatos Tower Search
					}
				}
				break;
			case 2:
				if (checkquest(13116,PLAYTIME) == 0)
					mes "Searching Rachel was enough. I will ask you again after a while.";
				else if (isbegin_quest(13112) == 1) {
					mes "How was your trip? No special news. But it is not done yet so I will ask you to search again.";
					callsub S_Quest,13112,13116,1000000,300000;
				}
				else if (isbegin_quest(13108) == 1)
					mes "Not able to enter the sanctuary in a usual way. It is not easy but we can find a clue at Rachel.";
				else {
					cutin "igu03",2;
					mes "There is a sanctuary inside of Rachel Shrine. It is possible to get stuck there once you're inside.";
					next;
					mes "[Aigu]";
					mes "Please take a look if there are any traces of lost adventurers at the southeast part of the 2nd floor in the middle zone.";
					next;
					if (select( "Begin the search.", "Stop." ) == 1) {
						cutin "igu05",2;
						mes "[Aigu]";
						mes "I wish you safety in your search.";
						callsub S_Quest,13116,13108;// Rachel holy ground Search
					}
				}
				break;
			case 3:
				if (checkquest(13117,PLAYTIME) == 0)
					mes "Searching Nameless Island will continue at another time. How about taking a rest for a while?";
				else if (isbegin_quest(13113) == 1) {
					mes "Thank you! There are wandering adventurers also helping! We should continue to search anyway. Please help me next time again.";
					callsub S_Quest,13113,13117,900000,300000;
				}
				else if (isbegin_quest(13109) == 1) {
					cutin "igu04",2;
					mes "Maybe... hanging out and drinking with the dead. But please talk with me.";
				}
				else {
					cutin "igu03",2;
					mes "Yes, Maybe they're not able to come because they've been captured by the dead! Nameless Island will be fine! Please search Nameless Island.";
					next;
					if (select( "Begin the search.", "Stop." ) == 1) {
						cutin "igu01",2;
						mes "[Aigu]";
						mes "I wil ask you to search the 1st underground of the monastery on Nameless Island. I cannot make you take that risk unless you volunteer.";
						callsub S_Quest,13117,13109;// Unknown island Search
					}
				}
				break;
			case 4:
				if (checkquest(13118,PLAYTIME) == 0)
					mes "I will try to search Abyss Lake again after a while.";
				else if (isbegin_quest(13114) == 1) {
					cutin "igu05",2;
					mes "You came back safely but I guess there was no good result for your face. I will search more so do not disappoint! Thank you.";
					callsub S_Quest,13114,13118,900000,200000;
				}
				else if (isbegin_quest(13110) == 1) {
					cutin "igu05",2;
					mes "Maybe, disappointed and fallen down because a monster was not found on the ground of the lake.";
				}
				else {
					mes "I suggest to search Abyss Lake precisely the cave at the middle of the lake.";
					next;
					mes "[Aigu]";
					mes "Yes! The end of a good adventure needs a dragon! I've heard there is a monster on the ground of a lake.";
					next;
					if (select( "Begin the search.", "Stop." ) == 1) {
						mes "[Aigu]";
						mes "Abyss Lake is the fastest way to get to Hugel. Please visit 3rd floor of underground cave.!";
						callsub S_Quest,13118,13110;// Abyss Lake Search
					}
				}
			}
		}
		else {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "I can't give you any more search tasks right now! You need harder challenges suitable for your level.";
			callsub S_Erasequest,
				13107,"Thanatos Tower",
				13108,"Rachel Shrine",
				13109,"Nameless Island",
				13110,"Abyss Lake";
			callsub S_Erasequest2,
				13111,"Thanatos Tower",1250000,300000,
				13112,"Rachel Shrine",1000000,300000,
				13113,"Nameless Island",900000,300000,
				13114,"Abyss Lake",900000,200000;
			for ( .@i = 13115; .@i <= 13118; .@i++ )
				if (isbegin_quest(.@i)) erasequest .@i;
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "Oh, you've been working on "+ getarg(.@i+1) +" Adventure task. Let me clear your record, so you can work on better tasks.";
			erasequest getarg(.@i);
		}
	}
	return;
S_Erasequest2:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 4 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "Oh, you've already finished "+ getarg(.@i+1) +" Adventure. Thank you for helping me out with this task!.";
			erasequest getarg(.@i);
			getexp getarg(.@i+2),getarg(.@i+3);
		}
	}
	return;
}

-	script	Trace of an adventurer	-1,{
	.@quest = 13106 + atoi(strnpcinfo(2));
	.@delay = 13110 + atoi(strnpcinfo(2));
	if (isbegin_quest(.@quest) == 1) {
		mes "I found traces of someone who was here but I cannot find a clue. Lets go back and report to Aigu.";
		erasequest .@quest;
		setquest .@delay;
	}
	else
		mes "There are traces of someone being here.";
	close;
}
thana_step,167,292,5	duplicate(Trace of an adventurer)	Trace of an adventurer#1	4_TRACE
ra_san05,265,69,5	duplicate(Trace of an adventurer)	Trace of an adventurer#2	4_TRACE
abbey02,155,290,5	duplicate(Trace of an adventurer)	Trace of an adventurer#3	4_TRACE
abyss_03,102,140,5	duplicate(Trace of an adventurer)	Trace of an adventurer#4	4_TRACE

tha_t06,126,128,3	script	Suspicious Monster#1	4_DEVIRUCHI,{
	mes "[Disguised Adventurer]";
	mes "I am just working at this dangerous place for 5000 zeny... Are you the adventurer who got a request from Eden Group?";
	next;
	switch( select( "Yes","No" ) ) {
		mes "[Disguised Adventurer]";
	case 1:
		if (BaseLevel < 121 || BaseLevel > 130) {
			mes "I could see that is not you at first glance!";
			close;
		}
		mes "The adventurers proceeding the request for Aigu are warped to the upper level on behalf of Aigu.";
		next;
		if (select( "Go up.", "Do not go up." ) == 1) {
			mes "[Disguised Adventurer]";
			if (isbegin_quest(13107) != 1)
				mes "It seems that you're currently not doing the Thanatos Tower adventure request. I'm sorry but I'm doing this for Aigu and not you.";
			else {
				mes "Be careful! You can not come down once you go up. For Aigu!";
				close2;
				warp "thana_step",69,369;
				end;
			}
		}
		close;
	case 2:
		mes "Or please pretend not to know me! I will be in a danger if you blow my cover!";
		close;
	}
}
