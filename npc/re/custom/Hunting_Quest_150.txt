//===== rAthena Script =======================================
//= saders hunting quest
//===== By: ==================================================
//= Sader1992
//https://rathena.org/board/profile/30766-sader1992/
//===== Current Version: =====================================
//= 5.2
//===== Compatible With: ===================================== 
//= rAthena Project
//https://rathena.org/board/files/file/3579-saders-hunting-quest/
//https://github.com/sader1992/sader_scripts
//===== Description: =========================================
//============================================================
//============================================================
//-	pointshop	Hunting_Quest_Shop	-1,#HUNT_Q_POINTS,512:1; //Account Based Shop
//-	pointshop	Hunting_Quest_Shop	-1,HUNT_Q_POINTS,512:1; //Character Based Shop
prontera,153,175,4	script	Ca�a LvL 150	857,{
	function Ac;
	mes "Seu progresso na miss�o:";
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "COM_" + .@i)){
			mes .CataName$[.@i] + ": Complete";
			setd Ac() + "POINTS",getd(Ac() + "POINTS") + .CataRP[.@i];
			if(.ExpServerRate){
				getexp .CataBXP[.@i],.CataJXP[.@i];
			}else{
				getexp2 .CataBXP[.@i],.CataJXP[.@i];
			}
			if(getd(".IRD_" + .@i)){
				for(.@j=0;.@j<getarraysize(getd(".IRD_" + .@i));.@j++){
					getitem(getd(".IRD_" + .@i + "[" + .@j + "]"),getd(".CRD_" + .@i + "[" + .@j + "]"));
				}
			}
			setd(Ac() + "CD_" + .@i,gettimetick(2) + (.CataCD[.@i] * 60 * 60));
			setd(Ac() + "COM_" + .@i,false);
			setd(Ac() + "ID_" + .@i,0);
			setd(Ac() + "C_" + .@i,0);
			setd(Ac() + "CC_" + .@i,0);
			continue;
		}
		.@CD = getd(Ac() + "CD_" + .@i);
		if(.@CD > gettimetick(2)){
			mes .CataName$[.@i] + ": " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD);
			continue;
		}
		if(getd(Ac() + "ID_" + .@i)){
			mes .CataName$[.@i] + "(" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@i));
			continue;
		}
		mes .CataName$[.@i] + ": None";
	}
	.@s1 = select(":" + implode(.CataName$,":")) -2;
	clear;
	if(.@s1 == -1){
		callshop "Hunting_Quest_Shop5",1;
		end;
	}
	.@CD = getd(Ac() + "CD_" + .@s1);
	if(.@CD > gettimetick(2)){
		mes "voc� est� sob o efeito do tempo de espera";
		mes "tente novamente ap�s " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD);
		end;
	}
	if(getd(Ac() + "ID_" + .@s1)){
		mes "Seu progresso na miss�o:";
		mes "(" + getd(Ac() + "CC_" + .@s1) + "/" + getd(Ac() + "C_" + .@s1) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@s1));
		if(.AllowDeleteQuest){
			mes "Voc� gostaria de excluir a miss�o??";
			if(select("Sim:N�o") == 2)
				end;
			mes "Ao excluir a miss�o, voc� n�o receber� nenhuma recompensa";
			if(.ApplyCoolDownOnDelete) mes "e o tempo de espera ser� aplicado!";
			mes "ATem certeza?!?";
			if(select("Sim:N�o") == 2)
				end;
			clear;
			if(.ApplyCoolDownOnDelete){
				setd(Ac() + "CD_" + .@s1,gettimetick(2) + (.CataCD[.@s1] * 60 * 60));
			}
			setd(Ac() + "ID_" + .@s1,0);
			setd(Ac() + "C_" + .@s1,0);
			setd(Ac() + "CC_" + .@s1,0);
			mes "sua miss�o foi exclu�da!";
			end;
		}
		end;
	}
	if(BaseLevel < .CataMN_L[.@s1] || BaseLevel > .CataMX_L[.@s1]){
		mes "voc� precisa estar entre os n�veis " + .CataMN_L[.@s1] + " E " + .CataMX_L[.@s1] + " para obter essa miss�o!";
		end;
	}
	mes "Voc� gostaria de receber uma miss�o??";
	if(select("Sim:N�o") == 2)
		end;
	copyarray(.@mid,getd(".m_id_" + .@s1),getarraysize(getd(".m_id_" + .@s1)));
	.@r = rand(getarraysize(.@mid));
	copyarray(.@mmn,getd(".m_mn_" + .@s1),getarraysize(getd(".m_mn_" + .@s1)));
	copyarray(.@mmx,getd(".m_mx_" + .@s1),getarraysize(getd(".m_mx_" + .@s1)));
	setd(Ac() + "ID_" + .@s1,.@mid[.@r]);
	setd(Ac() + "C_" + .@s1,rand(getd(".m_mn_" + .@s1 + "[" + .@r + "]"),getd(".m_mx_" + .@s1 + "[" + .@r + "]")));
	setd(Ac() + "CC_" + .@s1,0);
	mes "Por favor, ca�a:";
	mes "(" + getd(Ac() + "CC_" + .@s1) + "/" + getd(Ac() + "C_" + .@s1) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@s1));
end;

OnNPCKillEvent:
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "ID_" + .@i) == killedrid && !getd(Ac() + "COM_" + .@i)){
			setd(Ac() + "CC_" + .@i,getd(Ac() + "CC_" + .@i) + 1);
			dispbottom "Miss�o de ca�a [" + .CataName$[.@i] + "]: (" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo( 1,getd(Ac() + "ID_" + .@i));
			if(getd(Ac() + "CC_" + .@i) >= getd(Ac() + "C_" + .@i)){
				dispbottom "Seu " + .CataName$[.@i] + " A miss�o de ca�a est� conclu�da.";
				setd(Ac() + "COM_" + .@i,true);
			}
		}
	}
end;

OnHuntingCheck:
	dispbottom "Seu progresso na miss�o de ca�a:";
	for(.@i=0;.@i<getarraysize(.CataName$);.@i++){
		if(getd(Ac() + "COM_" + .@i)){
			dispbottom .CataName$[.@i] + ": Completo";
			continue;
		}
		.@CD = getd(Ac() + "CD_" + .@i);
		if(.@CD > gettimetick(2)){
			dispbottom .CataName$[.@i] + ": " + gettimestr("%Y-%m/%d %H:%M:%S",21,.@CD);
			continue;
		}
		if(getd(Ac() + "ID_" + .@i) == killedrid && !getd(Ac() + "COM_" + .@i)){
			dispbottom .CataName$[.@i] + "(" + getd(Ac() + "CC_" + .@i) + "/" + getd(Ac() + "C_" + .@i) + ") " + strmobinfo(1,getd(Ac() + "ID_" + .@i));
			continue;
		}
		dispbottom .CataName$[.@i] + ": None";
	}
end;

	function	Ac	{
		if(.AccountBase)
			return "#" + .variable_name$;
		return .variable_name$;
	}

	function	AddCata	{
		.@s = getarraysize(.CataName$);
		set getarg(1),.@s;
		.CataName$[.@s] = getarg(0);
		.CataMN_L[.@s] = getarg(2);
		.CataMX_L[.@s] = getarg(3);
		.CataCD[.@s] = getarg(4);
		.CataBXP[.@s] = getarg(5);
		.CataJXP[.@s] = getarg(6);
		.CataRP[.@s] = getarg(7);
		for(.@i=8;.@i<getargcount();.@i+=2){
			.@s2 = getarraysize(getd(".IRD_" + .@s));
			setd ".IRD_" + .@s + "[" + .@s2 + "]",getarg(.@i);
			setd ".CRD_" + .@s + "[" + .@s2 + "]",getarg(.@i+1);
		}
		return;
	}

	function	AddMob	{
		.@s = getarraysize(getd(".m_id_" + getarg(0)));
		setd ".m_id_" + getarg(0) + "[" + .@s + "]",getarg(1);
		setd ".m_mn_" + getarg(0) + "[" + .@s + "]",getarg(2);
		setd ".m_mx_" + getarg(0) + "[" + .@s + "]",getarg(3);
		return;
	}
	
OnInit:
	.AccountBase = false;//se as miss�es s�o ou n�o por conta ou por personagem
	.ExpServerRate = false;//receber exp de acordo com a taxa do servidor?
	.AllowDeleteQuest = true;//permitir que o jogador exclua sua miss�o sem receber a recompensa
	.ApplyCoolDownOnDelete = false;//aplicar ou n�o o tempo de espera ao excluir a miss�o.
	.variable_name$ = "HUNT_Q_";	//Lembre-se de que, se voc� alterar isso, precisar� alterar os pontos na loja!
	bindatcmd("MyQuest",strnpcinfo(3)+"::OnHuntingCheck",0,99);
	freeloop(1);
	//Voc� pode adicionar quantas categorias quiser!
	//recomendado n�o mais que 25!
	//AddCata(<"name">,<.var>,<mini level>,<max level>,<cooldown in hours>,<base exp reward>,<job exp reward>,<point reward>,<ITEM ID>,<COUNT>{,<ITEM ID>,<COUNT>{,<ITEM ID>,<COUNT>}})
	AddCata("Illus�o G�meos",.Easy,1,999,24,0,0,0,35005,50);
	
	//Voc� pode adicionar quantos mobs quiser!
	//AddMob(<.var>,<mob id>,<min count>,<max count>);
	AddMob(.Easy,1002,200,200);
	
end;
}